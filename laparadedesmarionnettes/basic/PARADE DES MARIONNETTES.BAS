0  ONERR  GOTO 10000
1  POKE 791,32
5 D$ =  CHR$ (4): TEXT : POKE  - 16300,0: POKE  - 16298,0
7  REM  MOPTOWN PARADE, COPYRIGHT 1981, LESLIE M. GRIMM
8  PRINT D$;"BLOAD MOPTOWN PARADE.LPIC,A$5000": GR : CALL 815
10  DIM P$(20),N(12)
20  DIM PP$(17):PP$(1) = "GGRB":PP$(2) = "GGRG":PP$(3) = "GGBB":PP$(4) = "GGBG":PP$(5) = "GMRB":PP$(6) = "GMRG":PP$(7) = "GMBB":PP$(8) = "GMBG"
22 PP$(9) = "PGRB":PP$(10) = "PGRG":PP$(11) = "PGBB":PP$(12) = "PGBG":PP$(13) = "PMRB":PP$(14) = "PMRG":PP$(15) = "PMBB":PP$(16) = "PMBG"
24 N(1) = 192:N(2) = 152:N(3) = 128:N(4) = 96:N(5) = 76:N(6) = 64:N(7) = 48:N(8) = 37:N(9) = 31:N(10) = 23
32  GOSUB 310
40  VTAB 24: PRINT "VEUX-TU DES INSTRUCTIONS? (O OU N) ";
42  GOSUB 60: IF  ASC (IN$) = 27 THEN  GOSUB 520:IN$ = ""
44  IF IN$ <  > "O" AND IN$ <  > "N" THEN  HOME : GOTO 40
46  IF IN$ = "O" THEN  GOSUB 2000
50  GOTO 1000
60  GET IN$: IF  LEN (IN$) < 1 THEN 60
62  RETURN 
100  COLOR= 14: PLOT X + 3,Y + 1: HLIN X + 3,X + 6 AT Y + 2: HLIN X + 3,X + 4 AT Y + 3: PLOT X + 6,Y + 3: HLIN X + 2,X + 6 AT Y + 14: PLOT X + 6,Y + 13: COLOR= 9: PLOT X + 4,Y + 1
102  COLOR= 1: VLIN Y + 4,Y + 13 AT X + 3: VLIN Y + 4,Y + 13 AT X + 4: VLIN Y + 5,Y + 8 AT X + 5: RETURN 
104  COLOR= 14: HLIN X + 2,X + 3 AT Y + 1: HLIN X + 2,X + 4 AT Y + 2: PLOT X + 3,Y + 3: HLIN X + 3,X + 5 AT Y + 14: PLOT X + 1,Y + 8: PLOT X + 2,Y + 9: COLOR= 9: PLOT X + 4,Y + 1
106  COLOR= 1: VLIN Y + 4,Y + 13 AT X + 3: VLIN Y + 4,Y + 13 AT X + 4: VLIN Y + 5,Y + 8 AT X + 5: RETURN 
110  COLOR= 14: PLOT X + 3,Y + 1: HLIN X + 3,X + 6 AT Y + 2: HLIN X + 3,X + 4 AT Y + 3: PLOT X + 6,Y + 3: HLIN X + 2,X + 6 AT Y + 14: PLOT X + 6,Y + 13: COLOR= 9: PLOT X + 4,Y + 1
112  COLOR= 6: VLIN Y + 4,Y + 13 AT X + 3: VLIN Y + 4,Y + 13 AT X + 4: VLIN Y + 5,Y + 8 AT X + 5: RETURN 
114  COLOR= 14: HLIN X + 2,X + 3 AT Y + 1: HLIN X + 2,X + 4 AT Y + 2: PLOT X + 3,Y + 3: HLIN X + 3,X + 5 AT Y + 14: PLOT X + 1,Y + 8: PLOT X + 2,Y + 9: COLOR= 9: PLOT X + 4,Y + 1
116  COLOR= 6: VLIN Y + 4,Y + 13 AT X + 3: VLIN Y + 4,Y + 13 AT X + 4: VLIN Y + 5,Y + 8 AT X + 5: RETURN 
120  COLOR= 14: PLOT X + 3,Y + 1: HLIN X + 3,X + 6 AT Y + 2: HLIN X + 3,X + 4 AT Y + 3: PLOT X + 6,Y + 3: HLIN X + 2,X + 6 AT Y + 14: PLOT X + 6,Y + 13: COLOR= 9: PLOT X + 4,Y + 1
122  COLOR= 1: VLIN Y + 4,Y + 13 AT X + 3: RETURN 
124  COLOR= 14: HLIN X + 2,X + 3 AT Y + 1: HLIN X + 2,X + 4 AT Y + 2: PLOT X + 3,Y + 3: HLIN X + 3,X + 5 AT Y + 14: PLOT X + 1,Y + 8: PLOT X + 2,Y + 9: COLOR= 9: PLOT X + 4,Y + 1
126  COLOR= 1: VLIN Y + 4,Y + 13 AT X + 3: RETURN 
130  COLOR= 14: PLOT X + 3,Y + 1: HLIN X + 3,X + 6 AT Y + 2: HLIN X + 3,X + 4 AT Y + 3: PLOT X + 6,Y + 3: HLIN X + 2,X + 6 AT Y + 14: PLOT X + 6,Y + 13: COLOR= 9: PLOT X + 4,Y + 1
132  COLOR= 6: VLIN Y + 4,Y + 13 AT X + 3: RETURN 
134  COLOR= 14: HLIN X + 2,X + 3 AT Y + 1: HLIN X + 2,X + 4 AT Y + 2: PLOT X + 3,Y + 3: HLIN X + 3,X + 5 AT Y + 14: PLOT X + 1,Y + 8: PLOT X + 2,Y + 9: COLOR= 9: PLOT X + 4,Y + 1
136  COLOR= 6: VLIN Y + 4,Y + 13 AT X + 3: RETURN 
140  COLOR= 14: HLIN X + 3,X + 6 AT Y + 5: HLIN X + 3,X + 4 AT Y + 6: PLOT X + 3,Y + 4: PLOT X + 6,Y + 6: HLIN X + 2,X + 6 AT Y + 14: PLOT X + 6,Y + 13: COLOR= 9: PLOT X + 4,Y + 4
142  COLOR= 1: VLIN Y + 7,Y + 13 AT X + 3: VLIN Y + 7,Y + 13 AT X + 4: VLIN Y + 8,Y + 10 AT X + 5: RETURN 
144  COLOR= 14: HLIN X + 2,X + 3 AT Y + 4: HLIN X + 2,X + 4 AT Y + 5: PLOT X + 3,Y + 6: HLIN X + 3,X + 5 AT Y + 14: PLOT X + 1,Y + 10: PLOT X + 2,Y + 11: COLOR= 9: PLOT X + 4,Y + 4
146  COLOR= 1: VLIN Y + 7,Y + 13 AT X + 3: VLIN Y + 7,Y + 13 AT X + 4: VLIN Y + 8,Y + 10 AT X + 5: RETURN 
150  COLOR= 14: HLIN X + 3,X + 6 AT Y + 5: HLIN X + 3,X + 4 AT Y + 6: PLOT X + 3,Y + 4: PLOT X + 6,Y + 6: HLIN X + 2,X + 6 AT Y + 14: PLOT X + 6,Y + 13: COLOR= 9: PLOT X + 4,Y + 4
152  COLOR= 6: VLIN Y + 7,Y + 13 AT X + 3: VLIN Y + 7,Y + 13 AT X + 4: VLIN Y + 8,Y + 10 AT X + 5: RETURN 
154  COLOR= 14: HLIN X + 2,X + 3 AT Y + 4: HLIN X + 2,X + 4 AT Y + 5: PLOT X + 3,Y + 6: HLIN X + 3,X + 5 AT Y + 14: PLOT X + 1,Y + 10: PLOT X + 2,Y + 11: COLOR= 9: PLOT X + 4,Y + 4
156  COLOR= 6: VLIN Y + 7,Y + 13 AT X + 3: VLIN Y + 7,Y + 13 AT X + 4: VLIN Y + 8,Y + 10 AT X + 5: RETURN 
160  COLOR= 14: HLIN X + 3,X + 6 AT Y + 5: HLIN X + 3,X + 4 AT Y + 6: PLOT X + 3,Y + 4: PLOT X + 6,Y + 6: HLIN X + 2,X + 6 AT Y + 14: PLOT X + 6,Y + 13: COLOR= 9: PLOT X + 4,Y + 4
162  COLOR= 1: VLIN Y + 7,Y + 13 AT X + 3: RETURN 
164  COLOR= 14: HLIN X + 2,X + 3 AT Y + 4: HLIN X + 2,X + 4 AT Y + 5: PLOT X + 3,Y + 6: HLIN X + 3,X + 5 AT Y + 14: PLOT X + 1,Y + 10: PLOT X + 2,Y + 11: COLOR= 9: PLOT X + 4,Y + 4
166  COLOR= 1: VLIN Y + 7,Y + 13 AT X + 3: RETURN 
170  COLOR= 14: HLIN X + 3,X + 6 AT Y + 5: HLIN X + 3,X + 4 AT Y + 6: PLOT X + 3,Y + 4: PLOT X + 6,Y + 6: HLIN X + 2,X + 6 AT Y + 14: PLOT X + 6,Y + 13: COLOR= 9: PLOT X + 4,Y + 4
172  COLOR= 6: VLIN Y + 7,Y + 13 AT X + 3: RETURN 
174  COLOR= 14: HLIN X + 2,X + 3 AT Y + 4: HLIN X + 2,X + 4 AT Y + 5: PLOT X + 3,Y + 6: HLIN X + 3,X + 5 AT Y + 14: PLOT X + 1,Y + 10: PLOT X + 2,Y + 11: COLOR= 9: PLOT X + 4,Y + 4
176  COLOR= 6: VLIN Y + 7,Y + 13 AT X + 3: RETURN 
180  COLOR= C1: FOR I = X TO X + 7: VLIN Y,Y + 14 AT I: NEXT I: RETURN 
182  COLOR= 13: HLIN X + 3,X + 6 AT Y + 6: VLIN Y + 7,Y + 8 AT X + 6: VLIN Y + 10,Y + 11 AT X + 4: PLOT X + 5,Y + 9: PLOT X + 4,Y + 13: RETURN 
191  COLOR= 13: VLIN Y + 1,Y + 5 AT X + 3: RETURN 
192  COLOR= 13: HLIN X + 2,X + 4 AT Y + 1: HLIN X + 2,X + 4 AT Y + 5: PLOT X + 4,Y + 2: PLOT X + 3,Y + 3: PLOT X + 2,Y + 4: RETURN 
193  COLOR= 13: HLIN X + 2,X + 4 AT Y + 1: HLIN X + 3,X + 4 AT Y + 3: HLIN X + 2,X + 4 AT Y + 5: VLIN Y + 2,Y + 4 AT X + 4: RETURN 
194  COLOR= 13: VLIN Y + 1,Y + 4 AT X + 2: VLIN Y + 1,Y + 5 AT X + 4: PLOT X + 3,Y + 4: PLOT X + 5,Y + 4: RETURN 
195  COLOR= 0: FOR I = Y TO Y + 5: HLIN X,X + 6 AT I: NEXT I: RETURN 
196 COLOR= 2: VLIN Y + 1,Y + 5 AT X + 1: VLIN Y + 4,Y + 5 AT X + 3: VLIN Y + 1,Y + 2 AT X + 3: PLOT X + 2,Y + 1: PLOT X + 2,Y + 3: VLIN Y + 1,Y + 5 AT X + 5: HLIN X + 6,X + 7 AT Y + 1: 
197 HLIN X + 6,X + 7 AT Y + 5: PLOT X + 6,Y + 3: HLIN X + 9,X + 11 AT Y + 1: VLIN Y + 1,Y + 5 AT X + 9: VLIN Y + 3,Y + 5 AT X + 11: PLOT X + 10,Y + 5: VLIN Y + 1,Y + 5 AT X + 13: 
198 HLIN X + 14,X + 15 AT Y + 5: VLIN Y + 1,Y + 5 AT X + 17: HLIN X + 18,X + 19 AT Y + 1: HLIN X + 18,X + 19 AT Y + 5: PLOT X + 18,Y + 3: PLOT X + 21,Y + 2: PLOT X + 21,Y + 4: RETURN
200  COLOR= 0: FOR I = 0 TO 10: HLIN 0,39 AT I: NEXT I: HLIN 0,39 AT 21
202 X = 0:Y = 22: IF  LEFT$ (P$(1),1) = "S" THEN Y = Y + 3
204  IF  MID$ (P$(1),2,1) = "T" THEN X = X - 1
206  COLOR= 8: HLIN X + 6,X + 8 AT Y + 7: HLIN X + 6,X + 8 AT Y + 8: COLOR= 14: HLIN X + 4,X + 6 AT Y + 6: PLOT X + 4,Y + 5: COLOR= 13: PLOT X + 7,Y + 5: PLOT X + 8,Y + 4
210 X = 8:Y = 22: IF  LEFT$ (P$(2),1) = "S" THEN Y = Y + 3
212  COLOR= 13: VLIN Y + 6,Y + 8 AT X + 8: VLIN Y + 5,Y + 6 AT X + 7: PLOT X + 6,Y + 4: PLOT X + 5,Y + 3: COLOR= 14: HLIN X + 4,X + 6 AT Y + 6
220 X = 16:Y = 22: IF  LEFT$ (P$(3),1) = "S" THEN Y = Y + 3
222  COLOR= 13: HLIN X + 5,X + 7 AT Y + 3: VLIN Y + 2,Y + 4 AT X + 8: COLOR= 14: HLIN X + 4,X + 6 AT Y + 6: VLIN Y + 3,Y + 6 AT X + 6
230 X = 24:Y = 22: IF  LEFT$ (P$(4),1) = "S" THEN Y = Y + 2
232  COLOR= 10: VLIN Y + 5,Y + 9 AT X + 7: COLOR= 15: PLOT X + 7,Y + 4: PLOT X + 7,Y + 10: COLOR= 14: HLIN X + 4,X + 7 AT Y + 7
240 X = 32:Y = 22: IF  LEFT$ (P$(5),1) = "S" THEN Y = Y + 2
242  COLOR= 8: VLIN Y - 7,Y + 9 AT X + 7: COLOR= 14: HLIN X + 4,X + 7 AT Y + 7: COLOR= 1: FOR I = Y - 7 TO Y - 4: HLIN X + 3,X + 6 AT I: NEXT I
244  COLOR= 4: HLIN X - 2,X + 2 AT Y - 7: HLIN X - 2,X + 2 AT Y - 5: HLIN X - 2,X + 6 AT Y - 3: HLIN X - 2,X + 6 AT Y - 1
246  COLOR= 15: HLIN X - 2,X + 2 AT Y - 6: HLIN X - 2,X + 2 AT Y - 4: HLIN X - 2,X + 6 AT Y - 2: COLOR= 13: PLOT X + 7,Y - 8
250  GOSUB 310: RETURN 
300  POKE 768,N: POKE 769,T: CALL 770: RETURN 
310  FOR I = 1 TO 11: READ N,T: GOSUB 300: NEXT I: RESTORE : RETURN 
312  DATA    54,140,64,140,81,120,81,120,81,120 
314  DATA  96,60,108,60,81, 80,72,100,85, 60,81,160 
320 T = 10:N = 64: GOSUB 300:N = 57: GOSUB 300:N = 51: GOSUB 300:N = 48: GOSUB 300: RETURN 
322 T = 80:N = 64: GOSUB 300:N = 96: GOSUB 300: RETURN 
400  IF P$ = "GGRB" THEN  GOSUB 100: RETURN 
402  IF P$ = "GGRG" THEN  GOSUB 104: RETURN 
404  IF P$ = "GGBB" THEN  GOSUB 110: RETURN 
406  IF P$ = "GGBG" THEN  GOSUB 114: RETURN 
408  IF P$ = "GMRB" THEN  GOSUB 120: RETURN 
410  IF P$ = "GMRG" THEN  GOSUB 124: RETURN 
412  IF P$ = "GMBB" THEN  GOSUB 130: RETURN 
414  IF P$ = "GMBG" THEN  GOSUB 134: RETURN 
416  IF P$ = "PGRB" THEN  GOSUB 140: RETURN 
418  IF P$ = "PGRG" THEN  GOSUB 144: RETURN 
420  IF P$ = "PGBB" THEN  GOSUB 150: RETURN 
422  IF P$ = "PGBG" THEN  GOSUB 154: RETURN 
424  IF P$ = "PMRB" THEN  GOSUB 160: RETURN 
426  IF P$ = "PMRG" THEN  GOSUB 164: RETURN 
428  IF P$ = "PMBB" THEN  GOSUB 170: RETURN 
430  IF P$ = "PMBG" THEN  GOSUB 174: RETURN 
500  GOSUB 60: IF IN$ = "" THEN 500
502  IF  ASC (IN$) = 27 THEN  GOSUB 520:IN$ = "ESC": RETURN 
504  IF IN$ = "?" THEN  GOSUB 2000: POP : POP : GOTO 1000
506  RETURN 
510  GOSUB 60: IF  ASC (IN$) <  > 13 AND  ASC (IN$) <  > 27 AND IN$ <  > "?" THEN 510
512  IF  ASC (IN$) = 27 THEN  GOSUB 520: RETURN 
514  IF IN$ = "?" THEN  GOSUB 2000: POP : GOTO 1000
516  RETURN 
520  HOME : PRINT "VEUX-TU T'ARRETER ? (O/N) ";
522  GET IN$: IF IN$ <  > "O" AND IN$ <  > "N" THEN 522
524  IF IN$ = "O" THEN 1500
526  RETURN 
530  GOSUB 60: IF IN$ = "?" THEN  GOSUB 2000: POP : GOTO 1000
532  IF  ASC (IN$) = 27 THEN  GOSUB 520:IN$ = "": RETURN 
534  RETURN 
600  POKE 34,20: HOME : PRINT "QUI VIENT ENSUITE ?": PRINT : PRINT "GRAND OU PETIT ? (G OU P) ";
602  GOSUB 500: IF IN$ = "ESC" THEN  GOTO 600
603  IF IN$ =  CHR$ (8) THEN 600: REM  ARROW
604  IF IN$ <  > "G" AND IN$ <  > "P" THEN 602
606 G1$ = IN$:GA$ = "GRAND": IF G1$ = "P" THEN GA$ = "PETIT"
608  HOME : VTAB 21: PRINT GA$: POKE 34,22
610  HOME : PRINT "GROS OU MINCE ? (G OU M) ";
612  GOSUB 500: IF IN$ = "ESC" THEN  GOTO 610
613  IF IN$ =  CHR$ (8) THEN 600
614  IF IN$ <  > "G" AND IN$ <  > "M" THEN 612
616 G2$ = IN$:GB$ = "GROS": IF G2$ = "M" THEN GB$ = "MINCE"
618  HOME : VTAB 21: PRINT GA$;", ";GB$
620  HOME : PRINT "ROUGE OU BLEU ? (R OU B) ";
622  GOSUB 500: IF IN$ = "ESC" THEN 620
623  IF IN$ =  CHR$ (8) THEN 600
624  IF IN$ <  > "R" AND IN$ <  > "B" THEN 622
626 G3$ = IN$:GC$ = "ROUGE": IF G3$ = "B" THEN GC$ = "BLEU"
628  HOME : VTAB 21: PRINT GA$;", ";GB$;", ";GC$
630  HOME : PRINT "BIBBIT OU GRIBBIT? (B OU G) ";
632  GOSUB 500: IF IN$ = "ESC" THEN  GOTO 630
633  IF IN$ =  CHR$ (8) THEN 600
634  IF IN$ <  > "B" AND IN$ <  > "G" THEN 632
636 G4$ = IN$:GD$ = "BIBBIT": IF G4$ = "G" THEN GD$ = "GRIBBIT"
638  HOME : VTAB 21: PRINT GA$;", ";GB$;", ";GC$;" ";GD$
640 GUESS$ = G1$ + G2$ + G3$ + G4$: RETURN 
1000  GR : HOME :RU = 1:C = 1
1002  COLOR= 13: HLIN 0,39 AT 21: HLIN 0,39 AT 37
1004 Y = 2:X = 5: GOSUB 196:X = 28: GOSUB 191
1006 X = 0:Y = 22
1010 A =  INT ( RND (1) * 16) + 1:P$(1) = PP$(A):P$ = P$(1): GOSUB 400:X = X + 8: GOSUB 182
1100  HOME : PRINT "VEUX-TU CHANGER CETTE REGLE ?": PRINT : PRINT "(TAPE O OU N) ";
1102  GOSUB 530: IF IN$ <  > "O" AND IN$ <  > "N" THEN 1100
1104  IF IN$ = "N" THEN  GOTO 1110
1106  HOME : PRINT "COMBIEN DE TRAITS VEUX-TU CHANGER ? ": PRINT : PRINT "(TAPE UN CHIFFRE DE 1 A 4) ";
1108  GOSUB 530:IN =  VAL (IN$): IF IN < 1 OR IN > 4 THEN 1106
1109 RU = IN:Y = 2:X = 28: GOSUB 195: ON IN GOSUB 191,192,193,194:X = 8:Y = 22
1110  GOSUB 600
1116 P$ = GUESS$: GOSUB 180: GOSUB 400: POKE 34,20
1120 FLAG = 0: FOR I = 1 TO 4: IF  MID$ (P$(C),I,1) <  >  MID$ (P$,I,1) THEN FLAG = FLAG + 1
1122  NEXT I: IF FLAG = RU THEN C = C + 1:P$(C) = P$: GOSUB 320: GOTO 1140
1130  GOSUB 322: HOME : PRINT "TON MOPPET EST DIFFERENT ";: IF FLAG = 1 THEN  PRINT "D'UNE FACON."
1132  IF FLAG <  > 1 THEN  PRINT "DE ";FLAG;" FACONS."
1136  VTAB 23: PRINT "APPUIE SUR <RETURN> ET RECOMMENCE. ";
1138  GOSUB 510: GOSUB 180: GOSUB 182: GOTO 1110
1140 X = X + 8: IF X > 32 AND Y < 18 THEN X = 0:Y = Y + 18
1142  IF X > 32 AND Y > 10 THEN 1200
1144  GOSUB 182: GOTO 1110
1200  HOME : PRINT : HTAB 11: PRINT "VOICI LA PARADE !": GOSUB 200
1210  PRINT : PRINT "VEUX-TU REJOUER ? (O OU N) ";
1212  GET IN$: IF IN$ <  > "O" AND IN$ <  > "N" THEN 1212
1214  IF IN$ = "O" THEN 1000
1500  TEXT : HOME : PRINT : POKE 791,2: PRINT  CHR$ (4);"RUN MENU"
2000  REM   ** INSTRUCTIONS **
2002  TEXT : HOME : VTAB 2: INVERSE : PRINT ">>>>>>>>>>>>>>>>>>>><<<<<<<<<<<<<<<<<<<<";
2004  VTAB 23: PRINT ">>>>>>>>>>>>>>>>>>>><<<<<<<<<<<<<<<<<<<<";: NORMAL 
2006  POKE 32,5: POKE 33,30: POKE 34,5: POKE 35,20: REM   WINDOW
2008  POKE 48,30: VLIN 4,43 AT 0: VLIN 4,43 AT 39: HOME 
2010  PRINT "LA PARADE DES MARIONNETTES !"
2012  PRINT : PRINT : PRINT "CHAQUE MOPPET DOIT ETRE":PRINT"DIFFERENT DU PRECEDENT."
2014  PRINT : PRINT "COMMENCE PAR CHOISIR UNE REGLEPOUR DEFINIR LE NOMBRE DE":PRINT"TRAITS DIFFERENTS DES MOPPETS":PRINT"(1, 2, 3 OU 4)."
2016  PRINT : PRINT "PUIS DECRIS LE MOPPET SUIVANT."
2055  FOR I = 1 TO 2000: NEXT I: POKE  - 16368,0
2060  VTAB 19: PRINT "APPUIE SUR <RETURN> POUR      JOUER. ";
2062  GOSUB 510: TEXT : HOME : RETURN 
10000  CALL 37632
