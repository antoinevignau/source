0  ONERR  GOTO 10000
1  POKE 791,32
5  REM  WHO COMES NEXT?, COPYRIGHT 1981, LESLIE M. GRIMM
10 D$ =  CHR$ (4)
20  DIM PP$(17):PP$(1) = "GGRB":PP$(2) = "GGRG":PP$(3) = "GGBB":PP$(4) = "GGBG":PP$(5) = "GMRB":PP$(6) = "GMRG":PP$(7) = "GMBB":PP$(8) = "GMBG"
22 PP$(9) = "PGRB":PP$(10) = "PGRG":PP$(11) = "PGBB":PP$(12) = "PGBG":PP$(13) = "PMRB":PP$(14) = "PMRG":PP$(15) = "PMBB":PP$(16) = "PMBG"
24  DIM N(9):N(1) = 96:N(2) = 76:N(3) = 85:N(4) = 72:N(5) = 64:N(6) = 57:N(7) = 51:N(8) = 48
35  GOSUB 3000: HOME 
40  PRINT : PRINT "VEUX-TU DES INSTRUCTIONS? (O OU N) ";
42  GOSUB 60: IF IN$ <  > "O" AND IN$ <  > "N" AND  ASC (IN$) <  > 27 THEN 42
44  IF  ASC (IN$) = 27 THEN  GOSUB 520: HOME : GOTO 40
46  IF IN$ = "O" THEN  GOSUB 2000
50  GOTO 1000
60  GET IN$: IF  LEN (IN$) < 1 THEN 60
62  RETURN 
100  COLOR= 14: PLOT X + 3,Y + 1: HLIN X + 3,X + 6 AT Y + 2: HLIN X + 3,X + 4 AT Y + 3: PLOT X + 6,Y + 3: HLIN X + 2,X + 6 AT Y + 14: PLOT X + 6,Y + 13: COLOR= 9: PLOT X + 4,Y + 1
102  COLOR= 1: VLIN Y + 4,Y + 13 AT X + 3: VLIN Y + 4,Y + 13 AT X + 4: VLIN Y + 5,Y + 8 AT X + 5: RETURN 
104  COLOR= 14: HLIN X + 2,X + 3 AT Y + 1: HLIN X + 2,X + 4 AT Y + 2: PLOT X + 3,Y + 3: HLIN X + 3,X + 5 AT Y + 14: PLOT X + 1,Y + 8: PLOT X + 2,Y + 9: COLOR= 9: PLOT X + 4,Y + 1
106  COLOR= 1: VLIN Y + 4,Y + 13 AT X + 3: VLIN Y + 4,Y + 13 AT X + 4: VLIN Y + 5,Y + 8 AT X + 5: RETURN 
110  COLOR= 14: PLOT X + 3,Y + 1: HLIN X + 3,X + 6 AT Y + 2: HLIN X + 3,X + 4 AT Y + 3: PLOT X + 6,Y + 3: HLIN X + 2,X + 6 AT Y + 14: PLOT X + 6,Y + 13: COLOR= 9: PLOT X + 4,Y + 1
112  COLOR= 6: VLIN Y + 4,Y + 13 AT X + 3: VLIN Y + 4,Y + 13 AT X + 4: VLIN Y + 5,Y + 8 AT X + 5: RETURN 
114  COLOR= 14: HLIN X + 2,X + 3 AT Y + 1: HLIN X + 2,X + 4 AT Y + 2: PLOT X + 3,Y + 3: HLIN X + 3,X + 5 AT Y + 14: PLOT X + 1,Y + 8: PLOT X + 2,Y + 9: COLOR= 9: PLOT X + 4,Y + 1
116  COLOR= 6: VLIN Y + 4,Y + 13 AT X + 3: VLIN Y + 4,Y + 13 AT X + 4: VLIN Y + 5,Y + 8 AT X + 5: RETURN 
120  COLOR= 14: PLOT X + 3,Y + 1: HLIN X + 3,X + 6 AT Y + 2: HLIN X + 3,X + 4 AT Y + 3: PLOT X + 6,Y + 3: HLIN X + 2,X + 6 AT Y + 14: PLOT X + 6,Y + 13: COLOR= 9: PLOT X + 4,Y + 1
122  COLOR= 1: VLIN Y + 4,Y + 13 AT X + 3: RETURN 
124  COLOR= 14: HLIN X + 2,X + 3 AT Y + 1: HLIN X + 2,X + 4 AT Y + 2: PLOT X + 3,Y + 3: HLIN X + 3,X + 5 AT Y + 14: PLOT X + 1,Y + 8: PLOT X + 2,Y + 9: COLOR= 9: PLOT X + 4,Y + 1
126  COLOR= 1: VLIN Y + 4,Y + 13 AT X + 3: RETURN 
130  COLOR= 14: PLOT X + 3,Y + 1: HLIN X + 3,X + 6 AT Y + 2: HLIN X + 3,X + 4 AT Y + 3: PLOT X + 6,Y + 3: HLIN X + 2,X + 6 AT Y + 14: PLOT X + 6,Y + 13: COLOR= 9: PLOT X + 4,Y + 1
132  COLOR= 6: VLIN Y + 4,Y + 13 AT X + 3: RETURN 
134  COLOR= 14: HLIN X + 2,X + 3 AT Y + 1: HLIN X + 2,X + 4 AT Y + 2: PLOT X + 3,Y + 3: HLIN X + 3,X + 5 AT Y + 14: PLOT X + 1,Y + 8: PLOT X + 2,Y + 9: COLOR= 9: PLOT X + 4,Y + 1
136  COLOR= 6: VLIN Y + 4,Y + 13 AT X + 3: RETURN 
140  COLOR= 14: HLIN X + 3,X + 6 AT Y + 5: HLIN X + 3,X + 4 AT Y + 6: PLOT X + 3,Y + 4: PLOT X + 6,Y + 6: HLIN X + 2,X + 6 AT Y + 14: PLOT X + 6,Y + 13: COLOR= 9: PLOT X + 4,Y + 4
142  COLOR= 1: VLIN Y + 7,Y + 13 AT X + 3: VLIN Y + 7,Y + 13 AT X + 4: VLIN Y + 8,Y + 10 AT X + 5: RETURN 
144  COLOR= 14: HLIN X + 2,X + 3 AT Y + 4: HLIN X + 2,X + 4 AT Y + 5: PLOT X + 3,Y + 6: HLIN X + 3,X + 5 AT Y + 14: PLOT X + 1,Y + 10: PLOT X + 2,Y + 11: COLOR= 9: PLOT X + 4,Y + 4
146  COLOR= 1: VLIN Y + 7,Y + 13 AT X + 3: VLIN Y + 7,Y + 13 AT X + 4: VLIN Y + 8,Y + 10 AT X + 5: RETURN 
150  COLOR= 14: HLIN X + 3,X + 6 AT Y + 5: HLIN X + 3,X + 4 AT Y + 6: PLOT X + 3,Y + 4: PLOT X + 6,Y + 6: HLIN X + 2,X + 6 AT Y + 14: PLOT X + 6,Y + 13: COLOR= 9: PLOT X + 4,Y + 4
152  COLOR= 6: VLIN Y + 7,Y + 13 AT X + 3: VLIN Y + 7,Y + 13 AT X + 4: VLIN Y + 8,Y + 10 AT X + 5: RETURN 
154  COLOR= 14: HLIN X + 2,X + 3 AT Y + 4: HLIN X + 2,X + 4 AT Y + 5: PLOT X + 3,Y + 6: HLIN X + 3,X + 5 AT Y + 14: PLOT X + 1,Y + 10: PLOT X + 2,Y + 11: COLOR= 9: PLOT X + 4,Y + 4
156  COLOR= 6: VLIN Y + 7,Y + 13 AT X + 3: VLIN Y + 7,Y + 13 AT X + 4: VLIN Y + 8,Y + 10 AT X + 5: RETURN 
160  COLOR= 14: HLIN X + 3,X + 6 AT Y + 5: HLIN X + 3,X + 4 AT Y + 6: PLOT X + 3,Y + 4: PLOT X + 6,Y + 6: HLIN X + 2,X + 6 AT Y + 14: PLOT X + 6,Y + 13: COLOR= 9: PLOT X + 4,Y + 4
162  COLOR= 1: VLIN Y + 7,Y + 13 AT X + 3: RETURN 
164  COLOR= 14: HLIN X + 2,X + 3 AT Y + 4: HLIN X + 2,X + 4 AT Y + 5: PLOT X + 3,Y + 6: HLIN X + 3,X + 5 AT Y + 14: PLOT X + 1,Y + 10: PLOT X + 2,Y + 11: COLOR= 9: PLOT X + 4,Y + 4
166  COLOR= 1: VLIN Y + 7,Y + 13 AT X + 3: RETURN 
170  COLOR= 14: HLIN X + 3,X + 6 AT Y + 5: HLIN X + 3,X + 4 AT Y + 6: PLOT X + 3,Y + 4: PLOT X + 6,Y + 6: HLIN X + 2,X + 6 AT Y + 14: PLOT X + 6,Y + 13: COLOR= 9: PLOT X + 4,Y + 4
172  COLOR= 6: VLIN Y + 7,Y + 13 AT X + 3: RETURN 
174  COLOR= 14: HLIN X + 2,X + 3 AT Y + 4: HLIN X + 2,X + 4 AT Y + 5: PLOT X + 3,Y + 6: HLIN X + 3,X + 5 AT Y + 14: PLOT X + 1,Y + 10: PLOT X + 2,Y + 11: COLOR= 9: PLOT X + 4,Y + 4
176  COLOR= 6: VLIN Y + 7,Y + 13 AT X + 3: RETURN 
180  COLOR= 0: FOR I = X TO X + 7: VLIN Y,Y + 14 AT I: NEXT I: RETURN 
182  COLOR= 13: HLIN X + 3,X + 6 AT Y + 6: VLIN Y + 7,Y + 8 AT X + 6: VLIN Y + 10,Y + 11 AT X + 4: PLOT X + 5,Y + 9: PLOT X + 4,Y + 13: RETURN 
190  COLOR=  INT ( RND (1) * 15) + 1: HLIN X,X + 7 AT Y - 2: HLIN X,X + 7 AT Y + 18: VLIN Y - 2,Y + 18 AT X: VLIN Y - 2,Y + 18 AT X + 7: RETURN 
192  COLOR= 0: HLIN X,X + 7 AT Y - 2: HLIN X,X + 7 AT Y + 18: VLIN Y - 2,Y + 18 AT X: VLIN Y - 2,Y + 18 AT X + 7: RETURN 
200  FOR I = 1 TO 200: NEXT I: COLOR= 0: HLIN 0,39 AT 14: HLIN 0,39 AT 30
202 T = 20:X =  - 8
210  FOR M = 1 TO 5
220 X = X + 8: GOSUB 190:N = N(M): GOSUB 300
224  FOR I = 1 TO 50: NEXT I
226  GOSUB 192
230  NEXT M
232  FOR M = 1 TO 5: GOSUB 190:A =  INT ( RND (1) * 5) + 1:N = N(A): GOSUB 300: NEXT M
240  RETURN 
300  POKE 768,N: POKE 769,T: CALL 770: RETURN 
400  IF P$ = "GGRB" THEN  GOSUB 100: RETURN 
402  IF P$ = "GGRG" THEN  GOSUB 104: RETURN 
404  IF P$ = "GGBB" THEN  GOSUB 110: RETURN 
406  IF P$ = "GGBG" THEN  GOSUB 114: RETURN 
408  IF P$ = "GMRB" THEN  GOSUB 120: RETURN 
410  IF P$ = "GMRG" THEN  GOSUB 124: RETURN 
412  IF P$ = "GMBB" THEN  GOSUB 130: RETURN 
414  IF P$ = "GMBG" THEN  GOSUB 134: RETURN 
416  IF P$ = "PGRB" THEN  GOSUB 140: RETURN 
418  IF P$ = "PGRG" THEN  GOSUB 144: RETURN 
420  IF P$ = "PGBB" THEN  GOSUB 150: RETURN 
422  IF P$ = "PGBG" THEN  GOSUB 154: RETURN 
424  IF P$ = "PMRB" THEN  GOSUB 160: RETURN 
426  IF P$ = "PMRG" THEN  GOSUB 164: RETURN 
428  IF P$ = "PMBB" THEN  GOSUB 170: RETURN 
430  IF P$ = "PMBG" THEN  GOSUB 174: RETURN 
500  GOSUB 60: IF IN$ = "" THEN 500
502  IF  ASC (IN$) = 27 THEN  GOSUB 520:IN$ = "ESC": RETURN 
504  IF (IN$) = "?" THEN  GOSUB 2000: POP : POP : GOTO 1000
506  RETURN 
520  HOME : PRINT "VEUX-TU T'ARRETER ? (O/N) ";
522  GET IN$: IF IN$ <  > "O" AND IN$ <  > "N" THEN 520
524  IF IN$ = "O" THEN  HOME : GOTO 1500
526  HOME : RETURN 
530  GOSUB 60: IF  ASC (IN$) <  > 13 AND  ASC (IN$) <  > 27 THEN 530
532  IF  ASC (IN$) = 27 THEN  GOSUB 520: RETURN 
534  RETURN 
540  GOSUB 60: IF  ASC (IN$) <  > 13 AND  ASC (IN$) <  > 27 AND IN$ <  > "?" THEN 540
542  IF  ASC (IN$) = 27 THEN  GOSUB 520: RETURN 
544  IF IN$ = "?" THEN  GOSUB 2000: POP : GOTO 1000
546  RETURN 
600  POKE 34,20: HOME : PRINT "QUI VIENT ENSUITE ?": PRINT : PRINT "GRAND OU PETIT ? (G OU P) ";
602  GOSUB 500: IF IN$ = "ESC" THEN  GOTO 600
603  IF IN$ <  > "G" AND IN$ <  > "P" THEN 602
604 G1$ = IN$:GA$ = "GRAND": IF G1$ = "P" THEN GA$ = "PETIT"
606  HOME : VTAB 21: PRINT GA$: POKE 34,22
610  HOME : PRINT "GROS OU MINCE ? (G OU M) ";
612  GOSUB 500: IF IN$ = "ESC" THEN  GOTO 610
613  IF IN$ =  CHR$ (8) THEN 600: REM  ARROW
614  IF IN$ <  > "G" AND IN$ <  > "M" THEN 612
616 G2$ = IN$:GB$ = "GROS": IF G2$ = "T" THEN GB$ = "MINCE"
618  HOME : VTAB 21: PRINT GA$;", ";GB$
620  HOME : PRINT "ROUGE OU BLEU ? (R OU B) ";
622  GOSUB 500: IF IN$ = "ESC" THEN 620
623  IF IN$ =  CHR$ (8) THEN 600: REM  ARROW
624  IF IN$ <  > "R" AND IN$ <  > "B" THEN 622
626 G3$ = IN$:GC$ = "ROUGE": IF G3$ = "B" THEN GC$ = "BLEU"
628  HOME : VTAB 21: PRINT GA$;", ";GB$;", ";GC$
630  HOME : PRINT "BIBBIT OU GRIBBIT? (B OU G) ";
632  GOSUB 500: IF IN$ = "ESC" THEN  GOTO 630
633  IF IN$ =  CHR$ (8) THEN 600: REM  ARROW
634  IF IN$ <  > "B" AND IN$ <  > "G" THEN 632
636 G4$ = IN$:GD$ = "BIBBIT": IF G4$ = "G" THEN GD$ = "GRIBBIT"
638  HOME : VTAB 21: PRINT GA$;", ";GB$;", ";GC$;" ";GD$
640 GUESS$ = G1$ + G2$ + G3$ + G4$: RETURN 
700  REM  ** DEFINE M$ FOR CLUE **
702 M1$ =  LEFT$ (M$,1): IF M1$ = "G" THEN M1$ = "GRAND, "
704  IF M1$ = "P" THEN M1$ = "PETIT, "
706 M2$ =  MID$ (M$,2,1): IF M2$ = "M" THEN M2$ = "MINCE, "
708  IF M2$ = "G" THEN M2$ = "GROS, "
710 M3$ =  MID$ (M$,3,1): IF M3$ = "B" THEN M3$ = "BLEU "
712  IF M3$ = "R" THEN M3$ = "ROUGE "
714 M4$ =  RIGHT$ (M$,1): IF M4$ = "B" THEN M4$ = "BIBBIT"
716  IF M4$ = "G" THEN M4$ = "GRIBBIT"
720 M$ = M1$ + M2$ + M3$ + M4$: RETURN 
1000  GR : HOME 
1002  COLOR= 13: HLIN 0,39 AT 14: HLIN 0,39 AT 30
1004 Y = 15:T = 40
1010 TS$ = "G":A =  RND (1): IF A < .5 THEN TS$ = "P"
1012 FT$ = "G":A =  RND (1): IF A < .5 THEN FT$ = "M"
1014 RB$ = "R":A =  RND (1): IF A < .5 THEN RB$ = "B"
1016 BG$ = "B":A =  RND (1): IF A < .5 THEN BG$ = "G"
1018 P$(1) = TS$ + FT$ + RB$ + BG$:M$ = P$(1): GOSUB 700:M$(1) = M$
1020 A =  INT ( RND (1) * 4) + 1: ON A GOTO 1022,1024,1026,1028
1022  IF TS$ = "G" THEN TS$ = "P": GOTO 1030
1023  IF TS$ = "P" THEN TS$ = "G": GOTO 1030
1024  IF FT$ = "G" THEN FT$ = "M": GOTO 1030
1025  IF FT$ = "M" THEN FT$ = "G": GOTO 1030
1026  IF RB$ = "R" THEN RB$ = "B": GOTO 1030
1027  IF RB$ = "B" THEN RB$ = "R": GOTO 1030
1028  IF BG$ = "B" THEN BG$ = "G": GOTO 1030
1029  IF BG$ = "G" THEN BG$ = "B"
1030 P$(2) = TS$ + FT$ + RB$ + BG$:M$ = P$(2): GOSUB 700:M$(2) = M$
1031 A =  INT ( RND (1) * 3) + 1: ON A GOTO 1032,1036,1040
1032 X = 0:P$ = P$(1): GOSUB 400:N = 96: GOSUB 300:X = 8:P$ = P$(2): GOSUB 400:N = 76: GOSUB 300:X = 16:P$ = P$(1): GOSUB 400:N = 96: GOSUB 300:X = 24:P$ = P$(2): GOSUB 400:N = 76: GOSUB 300
1034 X = 32: GOSUB 182:AN$ = P$(1):AN = 1:PA$ = "A B A B A B.": GOTO 1100
1036 P$ = P$(1):X = 0: GOSUB 400:N = 96: GOSUB 300:P$ = P$(2):X = 8: GOSUB 400:N = 76: GOSUB 300:P$ = P$(2):X = 16: GOSUB 400:N = 76: GOSUB 300:P$ = P$(1):X = 24: GOSUB 400:N = 96: GOSUB 300
1038 X = 32: GOSUB 182:AN$ = P$(2):AN = 2:PA$ = "A B B A B B.": GOTO 1100
1040 P$ = P$(1):X = 0: GOSUB 400:N = 96: GOSUB 300:P$ = P$(1):X = 8: GOSUB 400:N = 96: GOSUB 300:P$ = P$(2):X = 16: GOSUB 400:N = 76: GOSUB 300:P$ = P$(1):X = 24: GOSUB 400:N = 96: GOSUB 300
1042 X = 32: GOSUB 182:AN$ = P$(1):AN = 1:PA$ = "A A B A A B.": GOTO 1100
1100  GOSUB 600
1110  HOME :P$ = GUESS$: GOSUB 180: GOSUB 400
1120  IF GUESS$ = AN$ THEN N = N(AN): POKE 34,20: GOSUB 300: GOTO 1200
1122  IF GUESS$ <  > P$(1) AND GUESS$ <  > P$(2) THEN 1140
1130  POKE 34,21: HOME : PRINT "MA SEQUENCE EST ";PA$:N = 96:T = 40: GOSUB 300:N = 128:T = 80: GOSUB 300:T = 40
1132  VTAB 24: HTAB 1: PRINT "(APPUIE SUR <RETURN>.) ";
1134  GOSUB 540: GOSUB 180: GOSUB 182: POKE 34,20: HOME : GOTO 1100
1140  POKE 34,21: HOME : PRINT "CE DOIT ETRE ";M$(1): PRINT "OU UN ";M$(2);"."
1142 N = 96:T = 40: GOSUB 300:N = 128:T = 80: GOSUB 300:T = 40
1144  VTAB 24: HTAB 1: PRINT "(APPUIE SUR <RETURN>.) ";
1146  GOSUB 540: GOSUB 180: GOSUB 182: POKE 34,20: HOME : GOTO 1100
1200  HOME : PRINT "TU AS TROUVE !"
1202  GOSUB 200
1210  PRINT : PRINT "VEUX-TU REJOUER ? (O OU N) ";
1212  GET IN$: IF IN$ <  > "O" AND IN$ <  > "N" THEN 1212
1214  IF IN$ = "O" THEN 1000
1500  TEXT : HOME : PRINT : POKE 791,2: PRINT  CHR$ (4);"RUN MENU"
2000  REM   ** INSTRUCTIONS **
2002  TEXT : HOME : VTAB 2: INVERSE : PRINT ">>>>>>>>>>>>>>>>>>>><<<<<<<<<<<<<<<<<<<<";
2004  VTAB 23: PRINT ">>>>>>>>>>>>>>>>>>>><<<<<<<<<<<<<<<<<<<<";: NORMAL 
2006  POKE 32,5: POKE 33,30: POKE 34,5: POKE 35,20: REM   WINDOW
2008  POKE 48,30: VLIN 4,43 AT 0: VLIN 4,43 AT 39: HOME 
2010  PRINT "    QUI VIENT ENSUITE ?"
2012  PRINT : PRINT "LES MOPPETS SONT PLACES EN    L'UNE DES 3 SEQUENCES :"
2014  PRINT : PRINT "     1  A B A B A B"
2016  PRINT "     2  A B B A B B"
2018  PRINT "     3  A A B A A B"
2020  PRINT : PRINT "DETERMINE QUELLE SEQUENCE TU  VOIS."
2022  PRINT "CHOISIS QUI VIENT ENSUITE."
2030  FOR I = 1 TO 2000: NEXT 
2080  VTAB 19: PRINT "APPUIE SUR <RETURN> POUR      JOUER. ";
2082  GOSUB 530
2084  TEXT : HOME : RETURN 
3000  GR : HOME :Y = 15:P$(1) = "GGRB":P$(2) = "PMBG":T = 40
3010 X = 0:P$ = P$(1): GOSUB 400:N = 96: GOSUB 300:X = 8:P$ = P$(2): GOSUB 400:N = 76: GOSUB 300:X = 16:P$ = P$(1): GOSUB 400:N = 96: GOSUB 300:X = 24:P$ = P$(2): GOSUB 400:N = 76: GOSUB 300
3012 X = 32: GOSUB 182
3020  HTAB 10: PRINT "QUI VIENT ENSUITE ?"
3030  PRINT : PRINT "DETERMINE LA SEQUENCE QUE TU VOIS ET QUIVIENT ENSUITE."
3040  PRINT "APPUIE SUR <RETURN> QUAND TU ES PRET. ";
3042  GOSUB 530
3044  GOSUB 180:P$ = P$(1): GOSUB 400:N = 96: GOSUB 300
3050  HOME : RETURN 
10000  CALL 37632
