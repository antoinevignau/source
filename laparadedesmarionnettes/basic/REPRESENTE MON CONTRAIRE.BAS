0  ONERR  GOTO 10000
1  POKE 791,32
5 D$ =  CHR$ (4)
7  REM  MAKE MY OPPOSITE, COPYRIGHT 1983, LESLIE M GRIMM
10  TEXT : HOME 
12 ARROW$ =  CHR$ (8)
14 KB =  - 16384:SPACE = 32 + 128
30  POKE  - 16368,0: GOSUB 3000
32  VTAB 24: HTAB 1
40  PRINT "VEUX-TU DES INSTRUCTIONS? (O OU N) ";
42  GOSUB 80: IF IN$ <  > "O" AND  ASC (IN$) <  > 27 AND IN$ <  > "N" THEN 42
44  IF IN$ = "O" THEN  GOSUB 2000
45  IF  ASC (IN$) = 27 THEN 60
50  GOTO 1000
60  HOME : PRINT "DO YOU WANT TO STOP? (Y OR N) ";
62  GET IN$: IF IN$ <  > "Y" AND IN$ <  > "N" THEN 62
64  IF IN$ = "Y" THEN 1500
66  HOME : PRINT : GOTO 40
80  GET IN$: IF  LEN (IN$) < 1 THEN 80
82  RETURN 
100  COLOR= 14: VLIN Y + 1,Y + 3 AT X + 3: VLIN Y + 2,Y + 3 AT X + 4: HLIN X + 3,X + 5 AT Y + 14: COLOR= 9: PLOT X + 4,Y + 1: COLOR= 15: VLIN Y + 4,Y + 13 AT X + 3: VLIN Y + 4,Y + 13 AT X + 4: RETURN 
102  COLOR= 14: VLIN Y + 4,Y + 6 AT X + 3: VLIN Y + 5,Y + 6 AT X + 4: HLIN X + 3,X + 5 AT Y + 14: COLOR= 9: PLOT X + 4,Y + 4: COLOR= 15: VLIN Y + 7,Y + 13 AT X + 3: VLIN Y + 7,Y + 13 AT X + 4: RETURN 
110  COLOR= 15: IF G1$ = "GRAND" THEN  VLIN Y + 5,Y + 8 AT X + 5: RETURN 
112  VLIN Y + 8,Y + 10 AT X + 5: RETURN 
114  COLOR= 0: IF G1$ = "GRAND" THEN  VLIN Y + 4,Y + 13 AT X + 4: RETURN 
116  VLIN Y + 7,Y + 13 AT X + 4: RETURN 
120  COLOR= HUE: IF G1$ = "GRAND" AND G2$ = "GROS" THEN  VLIN Y + 4,Y + 13 AT X + 3: VLIN Y + 4,Y + 13 AT X + 4: VLIN Y + 5,Y + 8 AT X + 5: RETURN 
122  IF G1$ = "GRAND" AND G2$ = "MINCE" THEN  VLIN Y + 4,Y + 13 AT X + 3: RETURN 
124  IF G1$ = "PETIT" AND G2$ = "GROS" THEN  VLIN Y + 7,Y + 13 AT X + 3: VLIN Y + 7,Y + 13 AT X + 4: VLIN Y + 8,Y + 10 AT X + 5: RETURN 
126  IF G1$ = "PETIT" AND G2$ = "MINCE" THEN  VLIN Y + 7,Y + 13 AT X + 3: RETURN 
130  COLOR= 14: IF G1$ = "GRAND" THEN  VLIN Y + 1,Y + 2 AT X + 2: PLOT X + 1,Y + 8: PLOT X + 2,Y + 9: COLOR= 0: PLOT X + 4,Y + 3: RETURN 
134  COLOR= 14: VLIN Y + 4,Y + 5 AT X + 2: PLOT X + 1,Y + 10: PLOT X + 2,Y + 11: COLOR= 0: PLOT X + 4,Y + 6: RETURN 
140  COLOR= 14: IF G1$ = "GRAND" THEN  HLIN X + 5,X + 6 AT Y + 2: PLOT X + 6,Y + 3: VLIN Y + 13,Y + 14 AT X + 6: PLOT X + 2,Y + 14: RETURN 
142  HLIN X + 5,X + 6 AT Y + 5: PLOT X + 6,Y + 6: VLIN Y + 13,Y + 14 AT X + 6: PLOT X + 2,Y + 14: RETURN 
150 T = 5: FOR I = 1 TO 20: COLOR=  INT ( RND (1) * 15) + 1: HLIN X - 1,X + 8 AT Y - 1: VLIN Y - 1,Y + 16 AT X + 8: HLIN X - 1,X + 8 AT Y + 16: VLIN Y - 1,Y + 16 AT X - 1
152 P =  RND (1) * 200 + 10: GOSUB 300: NEXT I: RETURN 
160  COLOR= 15: HLIN X - 1,X + 8 AT Y - 1: VLIN Y - 1,Y + 16 AT X + 8: HLIN X - 1,X + 8 AT Y + 16: VLIN Y - 1,Y + 16 AT X - 1
162 T = 40:P = 96: GOSUB 300: GOSUB 300:T = 80:P = 192: GOSUB 300: RETURN 
164  COLOR= 15: HLIN X - 1,X + 8 AT Y - 1: VLIN Y - 1,Y + 16 AT X + 8: HLIN X - 1,X + 8 AT Y + 16: VLIN Y - 1,Y + 16 AT X - 1: RETURN 
170  COLOR= 0: FOR I = X TO X + 7: VLIN Y,Y + 16 AT I: NEXT I: RETURN 
180  COLOR= 0: FOR I = X + 1 TO X + 6: VLIN Y + 1,Y + 14 AT I: NEXT I: RETURN 
250  FOR I = 1 TO 700: NEXT I: RETURN 
300  POKE 768,P: POKE 769,T: CALL 770: RETURN 
400  GOSUB 80: IF IN$ = "?" THEN  POP : GOSUB 2000: GOTO 1000
402  IF  ASC (IN$) = 27 THEN  GOSUB 420
404  RETURN 
420  HOME : PRINT "VEUX-TU T'ARRETER ? (O OU N) ";
422  GET IN$: IF IN$ <  > "O" AND IN$ <  > "N" THEN 422
424  IF IN$ = "O" THEN 1500
426  HOME : RETURN 
1000 X = 8:Y = 15
1010  GR : HOME : POKE 34,22: HOME 
1020 A =  RND (1):TS$ = "GRAND": IF A < .5 THEN TS$ = "PETIT"
1022 A =  RND (1):FT$ = "GROS": IF A < .5 THEN FT$ = "MINCE"
1024 A =  RND (1):RB$ = "ROUGE": IF A < .5 THEN RB$ = "BLEU"
1026 A =  RND (1):BG$ = "BIBBIT": IF A < .5 THEN BG$ = "GRIBBIT"
1030 G1$ = TS$:G2$ = FT$:G3$ = RB$:G4$ = BG$
1032  IF TS$ = "GRAND" THEN  GOSUB 100:C1$ = "PETIT"
1034  IF TS$ = "PETIT" THEN  GOSUB 102:C1$ = "GRAND"
1036  IF FT$ = "GROS" THEN  GOSUB 110:C2$ = "MINCE"
1038  IF FT$ = "MINCE" THEN  GOSUB 114:C2$ = "GROS"
1040 HUE = 1:C3$ = "BLEU": IF RB$ = "BLEU" THEN HUE = 6:C3$ = "ROUGE"
1042  GOSUB 120
1044  IF BG$ = "GRIBBIT" THEN  GOSUB 130:C4$ = "UN BIBBIT"
1046  IF BG$ = "BIBBIT" THEN  GOSUB 140:C4$ = "UN GRIBBIT"
1050  GOSUB 164
1090 GUESS = 0:C = 0
1100 X = 24: POKE 34,20: HOME : GOSUB 180: GOSUB 164: POKE 34,22: HOME 
1102  PRINT "GRAND OU PETIT ? (G OU P) ";
1110  GOSUB 400:G1$ = IN$: IF G1$ = ARROW$ THEN 1100
1112  IF G1$ <  > "G" AND G1$ <  > "P" THEN 1100
1114  IF G1$ = "G" THEN G1$ = "GRAND": GOSUB 100
1116  IF G1$ = "P" THEN G1$ = "PETIT": GOSUB 102
1117  HOME : VTAB 21: PRINT G1$: POKE 34,22: HOME 
1118 T = 10:P = 192: GOSUB 300
1120  HOME : PRINT "GROS OU MINCE ? (G OU M) ";
1122  GOSUB 400:G2$ = IN$: IF G2$ = ARROW$ THEN 1100
1123  IF G2$ <  > "G" AND G2$ <  > "M" THEN 1120
1124  IF G2$ = "G" THEN G2$ = "GROS": GOSUB 110
1126  IF G2$ = "M" THEN G2$ = "MINCE": GOSUB 114
1127  HOME : VTAB 21: PRINT G1$", ";G2$
1128 T = 10:P = 171: GOSUB 300
1130  HOME : PRINT "ROUGE OU BLEU ? (R OU B) ";
1132  GOSUB 400:G3$ = IN$: IF G3$ = ARROW$ THEN 1100
1133  IF G3$ <  > "R" AND G3$ <  > "B" THEN 1130
1134  IF G3$ = "R" THEN G3$ = "ROUGE":HUE = 1: GOSUB 120
1136  IF G3$ = "B" THEN G3$ = "BLEU":HUE = 6: GOSUB 120
1137  HOME : VTAB 21: PRINT G1$", ";G2$", ";G3$
1138 T = 10:P = 152: GOSUB 300
1140  HOME : PRINT "BIBBIT OU GRIBBIT? (B OU G) ";
1142  GOSUB 400:G4$ = IN$: IF G4$ = ARROW$ THEN 1100
1143  IF G4$ <  > "B" AND G4$ <  > "G" THEN 1140
1144  IF G4$ = "B" THEN G4$ = "BIBBIT": GOSUB 140
1146  IF G4$ = "G" THEN G4$ = "GRIBBIT": GOSUB 130
1147  HOME : VTAB 21: PRINT G1$", ";G2$;", ";G3$" ";G4$
1148 T = 10:P = 144: GOSUB 300
1150  IF G1$ <  > TS$ AND G2$ <  > FT$ AND G3$ <  > RB$ AND G4$ <  > BG$ THEN  FOR I = 1 TO 500: NEXT : GOTO 1200
1160  FOR I = 1 TO 500: NEXT I: GOSUB 162: POKE 34,21
1162  FOR I = 1 TO 4:CL$(I) = "": NEXT :K = 0
1164  HOME : PRINT "ON LE FAIT ";
1166  IF G1$ = TS$ THEN K = K + 1:CL$(K) = C1$
1168  IF G2$ = FT$ THEN K = K + 1:CL$(K) = C2$
1170  IF G3$ = RB$ THEN K = K + 1:CL$(K) = C3$
1172  IF G4$ = BG$ THEN K = K + 1:CL$(K) = C4$
1174  IF K > 1 THEN 1178
1176  PRINT CL$(K);".": GOTO 1190
1178  IF K > 2 THEN 1182
1180  PRINT CL$(1);" AND ";CL$(2);".": GOTO 1190
1182  IF K > 3 THEN 1186
1184  PRINT CL$(1)", ";CL$(2);" ET ";CL$(3);".": GOTO 1190
1186  PRINT CL$(1);", ";CL$(2);", ";CL$(3);" ET":PRINT CL$(4);"."
1190  VTAB 24: HTAB 1: PRINT "(APPUIE SUR <RETURN>.) ";
1192  GOSUB 400: IF IN$ <  >  CHR$ (13) THEN  VTAB 24: HTAB 1: CALL  - 958: GOTO 1190
1196  POKE 34,20: HOME : GOSUB 180: GOTO 1100
1200  HOME : PRINT "TU AS CREE MON CONTRAIRE !": GOSUB 150
1210  PRINT : PRINT "VEUX-TU REJOUER ? (O OU N) ";
1212  GET IN$: IF IN$ <  > "O" AND IN$ <  > "N" THEN 1212
1214  IF IN$ = "O" THEN 1000
1216  GOTO 1500
1500  TEXT : HOME : PRINT : POKE 791,2: PRINT  CHR$ (4);"RUN MENU"
1550  POKE 34,20: HOME : PRINT "VEUX-TU T'ARRETER ? (O OU N) ";
1552  GET IN$: IF IN$ <  > "O" AND IN$ <  > "N" THEN 1552
1554  IF IN$ = "O" THEN 1500
1556  HOME : GOSUB 170: GOTO 1100
2000  REM   ** INSTRUCTIONS **
2002  TEXT : HOME : VTAB 2: INVERSE : PRINT ">>>>>>>>>>>>>>>>>>>><<<<<<<<<<<<<<<<<<<<";
2004  VTAB 23: PRINT ">>>>>>>>>>>>>>>>>>>><<<<<<<<<<<<<<<<<<<<";: NORMAL 
2006  POKE 32,5: POKE 33,30: POKE 34,5: POKE 35,20: REM   WINDOW
2008  POKE 48,30: VLIN 4,43 AT 0: VLIN 4,43 AT 39: HOME 
2010  PRINT "  REPRESENTE MON CONTRAIRE !"
2012  PRINT : PRINT : PRINT "TU VAS VOIR L'IMAGE D'UN      MOPPET."
2014  PRINT :         PRINT "CREE SON CONTRAIRE DANS       LE CADRE DE DROITE."
2016  PRINT :         PRINT "L'IMAGE CLIGNOTERA QUAND      SON CONTRAIRE AURA ETE CREE."
2025  FOR I = 1 TO 2000: NEXT I: POKE  - 16368,0
2030  VTAB 19: PRINT "APPUIE SUR <RETURN> POUR      JOUER. ";
2032  GOSUB 80: IF  ASC (IN$) <  > 13 AND  ASC (IN$) <  > 27 THEN 2032
2034  IF  ASC (IN$) = 13 THEN  TEXT : HOME : RETURN 
2040  HOME : PRINT "VEUX-TU T'ARRETER ? (O/N) ";
2042  GET IN$: IF IN$ <  > "O" AND IN$ <  > "N" THEN 2042
2044  IF IN$ = "O" THEN 1500
2046  TEXT : HOME : RETURN 
3000  GR : HOME : HTAB 8: PRINT "VOICI MON CONTRAIRE !"
3002 X = 10:Y = 15: GOSUB 164:X = 24: GOSUB 164:X = 10: GOSUB 100:G1$ = "GRAND":G2$ = "MINCE":G3$ = "ROUGE":HUE = 1:G4$ = "GRIBBIT": GOSUB 114: GOSUB 120: GOSUB 130
3004  IF  PEEK (KB) = SPACE THEN 3092
3006  GOSUB 250
3010 X = 24: GOSUB 164: GOSUB 102
3020  VTAB 23: HTAB 2: PRINT "PETIT";
3021 G1$ = "PETIT":G2$ = "GROS":HUE = 6: REM  SHORT FAT BLUE BIBBIT
3022 T = 10:P = 192: GOSUB 300
3024  IF  PEEK (KB) = SPACE THEN 3092
3026  GOSUB 250
3030  GOSUB 110
3040  HTAB 11: PRINT "GROS";
3042 T = 10:P = 171: GOSUB 300
3044  IF  PEEK (KB) = SPACE THEN 3092
3046  GOSUB 250
3050  GOSUB 120
3060  HTAB 21: PRINT "BLEU";
3062 T = 10:P = 152: GOSUB 300:
3064  IF  PEEK (KB) = SPACE THEN 3092
3066  GOSUB 250
3070  GOSUB 140
3080  HTAB 31: PRINT "BIBBIT"
3082 T = 10:P = 144: GOSUB 300
3084  IF  PEEK (KB) = SPACE THEN 3092
3086  GOSUB 250: GOSUB 150
3092  REM 
3094  RETURN 
10000  CALL 37632
